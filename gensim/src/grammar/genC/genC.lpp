%{

#include <iostream>
#include <stdint.h>

#define yyerminate() return ENDOFFILE

#include "flexbison_harness.h"
#include "genC.tabs.h"

%}

%option reentrant bison-bridge

%option noyywrap
%option yylineno

%x GENC_COMMENT
%x GENC_LINE_COMMENT
%x GENC_STRING

%%

"/*" BEGIN(GENC_COMMENT);
<GENC_COMMENT>"*/" BEGIN(INITIAL);
<GENC_COMMENT>. ;
<GENC_COMMENT>[ \t\r\n] ;

"//" BEGIN(GENC_LINE_COMMENT);
<GENC_LINE_COMMENT>"\n" BEGIN(INITIAL);
<GENC_LINE_COMMENT>. ;

[ \t\r\n]+ {}
";" return SEMICOLON;
":" return GENC_COLON;

"typename" return GENC_TYPENAME;
"struct" return GENC_STRUCT;
"helper" return GENC_HELPER;
"execute" return GENC_EXECUTE;

"internal" { yylval->sval = strdup(yytext); return GENC_HELPER_SCOPE; }
"public" { yylval->sval = strdup(yytext); return GENC_HELPER_SCOPE; }

"noinline" return GENC_HELPER_ATTRIBUTE;
"export" return GENC_HELPER_ATTRIBUTE;
"global" return GENC_HELPER_ATTRIBUTE;
"inline" return GENC_HELPER_ATTRIBUTE;

"return" return GENC_RETURN;
"switch" return GENC_SWITCH;
"case" return GENC_CASE;
"default" return GENC_DEFAULT;
"continue" return GENC_CONTINUE;
"break" return GENC_BREAK;
"raise" return GENC_RAISE;
"do" return GENC_DO;
"for" return GENC_FOR;
"while" return GENC_WHILE;
"if" return GENC_IF;
"else" return GENC_ELSE;

"&" return GENC_AMPERSAND;
"[" return GENC_OPEN_BRACKET;
"]" return GENC_CLOSE_BRACKET;
"(" return GENC_OPEN_PAREN;
")" return GENC_CLOSE_PAREN;
"{" return GENC_OPEN_BRACE;
"}" return GENC_CLOSE_BRACE;

"=" return GENC_EQUALS;
"." return GENC_PERIOD;
"," return GENC_COMMA;
"-" return GENC_MINUS;
"+" return GENC_PLUS;
"~" return GENC_TILDE;
"<<" return GENC_SHIFTLEFT;
">>" return GENC_SHIFTRIGHT;
">>>" return GENC_ROTATERIGHT;
"<<<" return GENC_ROTATELEFT;
"*" return GENC_ASTERIX;
"/" return GENC_FORWARDSLASH;
"!" return GENC_EXCLAMATION;
"|" return GENC_PIPE;
"^" return GENC_CARET;
"%" return GENC_PERCENT;
"?" return GENC_QUESTIONMARK;

"&&" return GENC_LOGICAL_AND;
"||" return GENC_LOGICAL_OR;

"|=" return GENC_PIPE_ASSIGN;
"&=" return GENC_AMPERSAND_ASSIGN;
"^=" return GENC_CARET_ASSIGN;
"+=" return GENC_PLUS_ASSIGN;
"-=" return GENC_MINUS_ASSIGN;
"*=" return GENC_ASTERIX_ASSIGN;
"/=" return GENC_DIVIDE_ASSIGN;
"%=" return GENC_PERCENT_ASSIGN;
"<<=" return GENC_LEFTSHIFT_ASSIGN;
">>=" return GENC_RIGHTSHIFT_ASSIGN;

"==" return GENC_COMPARE_EQUALITY;
"!=" return GENC_COMPARE_INEQUALITY;
">" return GENC_RIGHT_CHEVRON;
">=" return GENC_COMPARE_GTE;
"<" return GENC_LEFT_CHEVRON;
"<=" return GENC_COMPARE_LTE;

"++" return GENC_INCREMENT;
"--" return GENC_DECREMENT;

"uint8" { yylval->sval = strdup(yytext); return GENC_BASICTYPE; }
"uint16" { yylval->sval = strdup(yytext); return GENC_BASICTYPE; }
"uint32" { yylval->sval = strdup(yytext); return GENC_BASICTYPE; }
"uint64" { yylval->sval = strdup(yytext); return GENC_BASICTYPE; }
"uint128" { yylval->sval = strdup(yytext); return GENC_BASICTYPE; }
"sint8" { yylval->sval = strdup(yytext); return GENC_BASICTYPE; }
"sint16" { yylval->sval = strdup(yytext); return GENC_BASICTYPE; }
"sint32" { yylval->sval = strdup(yytext); return GENC_BASICTYPE; }
"sint64" { yylval->sval = strdup(yytext); return GENC_BASICTYPE; }
"sint128" { yylval->sval = strdup(yytext); return GENC_BASICTYPE; }
"float" { yylval->sval = strdup(yytext); return GENC_BASICTYPE; }
"double" { yylval->sval = strdup(yytext); return GENC_BASICTYPE; }
"longdouble" { yylval->sval = strdup(yytext); return GENC_BASICTYPE; }
"void" { yylval->sval = strdup(yytext); return GENC_BASICTYPE; }

[a-zA-Z_][a-zA-Z0-9_]* { yylval->sval = strdup(yytext); return GENC_ID; }

[0-9]+"U"?"L"* { yylval->ival = strtol(yytext, nullptr, 0); return GENC_INT_CONST; }
"0x"[0-9a-fA-F]+"U"?"L"* { yylval->ival = strtol(yytext, nullptr, 0); return GENC_INT_CONST; }
[0-9]+\.[0-9]*"f" { yylval->fval = strtof(yytext, nullptr); return GENC_FLOAT_CONST; }
[0-9]+\.[0-9]* { yylval->dval = strtod(yytext, nullptr); return GENC_DOUBLE_CONST; }

%%
