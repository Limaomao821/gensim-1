%{

#include <stdint.h>
#include "flexbison_harness.h"
#include "flexbison_archc_ast.h"

#define YYERROR_VERBOSE 1
%}

%code requires {
    namespace ArchC {
        class ArchCScanner;
    }
}

%skeleton "lalr1.cc"

%defines
%locations
%define api.namespace {ArchC}
%define api.prefix {ArchC}
%define parser_class_name {ArchCParser}

%union {
    uint64_t ival;
    char *sval;
    float fval;
    double dval;
    astnode<ArchCNodeType> *ast_node;
}

%parse-param { ArchCScanner &scanner } { astnode<ArchCNodeType> * root }

%code {
#include "flexbison_archc.h"
#undef yylex 
#define yylex scanner.ArchClex
}

%token AC_ARCH AC_FORMAT AC_WORDSIZE AC_FETCHSIZE AC_PREDICATED AC_MEM AC_INCLUDE AC_REGSPACE AC_REG_VIEW_SLOT AC_REG_VIEW_BANK AC_PC ARCH_CTOR ARCH_ISA 
%token SET_ENDIAN AC_ISA_BLOCK AC_INSTR AC_FIELD AC_BEHAVIOUR ISA_CTOR AC_ASM_MAP SET_ASM SET_DECODER SET_BEHAVIOUR SET_END_OF_BLOCK SET_JUMP_FIXED 
%token SET_JUMP_FIXED_PREDICATED SET_JUMP_VARIABLE SET_USES_PC SET_BLOCK_COND FIXED RELATIVE AC_BEHAVIOURS AC_DECODE AC_EXECUTE 
%token AC_TYPENAME AC_CONSTANT AC_FEATURES AC_FEATURE_LEVEL AC_FEATURE_FLAG AC_SET_FEATURE AC_STRUCT 

%token OPAREN CPAREN
%token OBRACKET CBRACKET
%token OANGLE CANGLE
%token OBRACE CBRACE
%token GROUPING

%token AC_AT

%token AC_PLUS AC_MINUS AC_ASTERIX

%token COMMA PERIOD SEMICOLON
%token EQUALS

%token <sval> AC_ID
%token <sval> AC_INT
%token <sval> AC_STRING

%type <ast_node> int string id
%type <ast_node> file arch_file isa_file arch_resource_list arch_resource arch_ctor
%type <ast_node> ar_wordsize ar_mem ar_regspace ar_pc ar_features 
%type <ast_node> format ar_reg_view_list ar_reg_view
%type <ast_node> arch_ctor_resource_list arch_ctor_resource
%type <ast_node> ac_isa ac_endianness ac_feature_set ac_typename ac_constant

%define parse.assert
%define parse.error verbose

%%

file : 
    arch_file { root->AddChild($1); } | 
    isa_file { root->AddChild($1); }

int : AC_INT { $$ = CreateNode(ArchCNodeType::INT, {CreateStrNode<ArchCNodeType>($1)}); }
string : AC_STRING { $$ = CreateStrNode<ArchCNodeType>($1); }
id : AC_ID { $$ = CreateNode(ArchCNodeType::Identifier, {CreateStrNode<ArchCNodeType>($1)}); }

arch_file : AC_ARCH OPAREN id CPAREN OBRACE arch_resource_list arch_ctor CBRACE SEMICOLON { $$ = CreateNode(ArchCNodeType::Arch, {$3, $6, $7}); }

arch_resource_list :
    arch_resource_list arch_resource { $$ = $1; $$->AddChild($2); }  |
    %empty { $$ = CreateNode(ArchCNodeType::List); }

arch_resource :
    ar_wordsize | 
    ar_mem | 
    ar_regspace | 
    ar_pc | 
    ar_features;

ar_wordsize :
    AC_WORDSIZE int SEMICOLON { $$ = CreateNode(ArchCNodeType::WordSize, {$2}); }

format :
    AC_FORMAT id EQUALS string SEMICOLON { $$ = CreateNode(ArchCNodeType::Format, {$2, $4}); }

ar_mem :
    AC_MEM id OPAREN int COMMA int COMMA id COMMA int CPAREN SEMICOLON { $$ = CreateNode(ArchCNodeType::MemInterface, {$2, $4, $6, $8, $10}); } 

ar_regspace :
    AC_REGSPACE OPAREN int CPAREN OBRACE ar_reg_view_list CBRACE { $$ = CreateNode(ArchCNodeType::RegSpace, {$3, $6}); }

ar_reg_view_list :
    ar_reg_view_list ar_reg_view {$$ = $1; $$->AddChild($2); }  |
    %empty { $$ = CreateNode(ArchCNodeType::List); };

ar_reg_view :
    AC_REG_VIEW_BANK id OPAREN id COMMA int COMMA int COMMA int COMMA int COMMA int COMMA int CPAREN SEMICOLON { $$ = CreateNode(ArchCNodeType::RegViewBank); } |
    AC_REG_VIEW_SLOT id OPAREN id COMMA int COMMA int CPAREN ar_reg_view_tag SEMICOLON { $$ = CreateNode(ArchCNodeType::RegViewSlot); } 

ar_reg_view_tag :
    id |
    %empty;

ar_pc :
    AC_PC id OPAREN int CPAREN SEMICOLON;

ar_features :
    AC_FEATURES OBRACE ar_feature_list CBRACE;

ar_feature_list :
    ar_feature_list ar_feature |
    %empty;

ar_feature :
    AC_FEATURE_LEVEL id SEMICOLON;

arch_ctor : ARCH_CTOR OPAREN id CPAREN OBRACE arch_ctor_resource_list CBRACE SEMICOLON { $$ = CreateNode(ArchCNodeType::ArchCtor, {$3, $6}); }

arch_ctor_resource_list :
    arch_ctor_resource_list arch_ctor_resource SEMICOLON { $$ = $1; $$->AddChild($2); } |
    %empty { $$ = CreateNode(ArchCNodeType::List); };

arch_ctor_resource :
    ac_isa |
    ac_endianness |
    ac_feature_set |
    ac_typename |
    ac_constant;

ac_isa :
    ARCH_ISA OPAREN string CPAREN { $$ = CreateNode(ArchCNodeType::AcIsa, {$3}); }

ac_endianness :
    SET_ENDIAN OPAREN string CPAREN  { $$ = CreateNode(ArchCNodeType::AcEndianness, {$3}); }

ac_feature_set :
    AC_SET_FEATURE OPAREN id COMMA int CPAREN  { $$ = CreateNode(ArchCNodeType::SetFeature, {$3, $5}); }

ac_typename :
    AC_TYPENAME OPAREN id COMMA id CPAREN  { $$ = CreateNode(ArchCNodeType::AcTypename, {$3, $5}); }

ac_constant :
    AC_CONSTANT OPAREN id COMMA id COMMA int CPAREN  { $$ = CreateNode(ArchCNodeType::AcConstant, {$3, $5, $7}); }

isa_file : AC_ISA_BLOCK OPAREN id CPAREN OBRACE isa_resource_list CBRACE SEMICOLON;

isa_resource_list :
    isa_resource_list isa_resource SEMICOLON |
    %empty;

isa_resource :
    ar_fetchsize |
    ar_include |
    format |
    isa_instruction |
    isa_field |
    isa_behaviour |
    isa_CTOR |
    isa_asm_map |
    ar_predicated |
    isa_struct_definition ;

ar_fetchsize :
    AC_FETCHSIZE int;

ar_include :
    AC_INCLUDE OPAREN string CPAREN;

format :
    def_attr_list AC_FORMAT id EQUALS string;

def_attr_list :
    def_attr_list_nonempty |
    %empty;

def_attr_list_nonempty :
    def_attr_list_nonempty def_attr |
    def_attr;

def_attr :
    AC_AT id;

isa_instruction :
    AC_INSTR OANGLE id CANGLE id_list;

isa_field :
    AC_FIELD OANGLE field_type CANGLE id;

field_type :
    id |
    id AC_ASTERIX;

isa_behaviour :
    AC_BEHAVIOUR id_list;

isa_CTOR :
    ISA_CTOR OPAREN id CPAREN OBRACE ctor_resource_list CBRACE;

ctor_resource_list :
    ctor_resource_list ctor_resource SEMICOLON |
    %empty;

ctor_resource :
    instr_resource |
    behaviours_resource |
    decodes_resource |
    execute_resource;

instr_resource :
    decoder_resource |
    assembler_resource |
    behaviour_resource |
    end_of_block_resource |
    jump_variable_resource |
    jump_fixed_resource |
    jump_fixed_predicated_resource |
    uses_pc_resource |
    block_condition_resource;

behaviours_resource :
    AC_BEHAVIOURS OPAREN string CPAREN;

decodes_resource :
    AC_DECODE OPAREN string CPAREN;

execute_resource :
    AC_EXECUTE OPAREN string CPAREN;

decoder_resource :
    id PERIOD SET_DECODER OPAREN expression_list CPAREN;

assembler_resource :
    id PERIOD SET_ASM OPAREN string asm_arg_list CPAREN;

behaviour_resource :
    id PERIOD SET_BEHAVIOUR OPAREN id CPAREN;

end_of_block_resource :
    id PERIOD SET_END_OF_BLOCK OPAREN expression_list CPAREN;

jump_variable_resource :
    id PERIOD SET_JUMP_VARIABLE OPAREN CPAREN;

jump_fixed_resource :
    id PERIOD SET_JUMP_FIXED OPAREN id COMMA fixed_jump_type COMMA int CPAREN;

fixed_jump_type :
    FIXED |
    RELATIVE;

jump_fixed_predicated_resource :
    id PERIOD SET_JUMP_FIXED_PREDICATED OPAREN id COMMA fixed_jump_type COMMA int CPAREN;

uses_pc_resource :
    id PERIOD SET_USES_PC OPAREN expression_list CPAREN;

block_condition_resource :
    id PERIOD SET_BLOCK_COND OPAREN CPAREN;

asm_arg_list :
    COMMA asm_arg |
    %empty;

asm_arg :
    expression;

isa_asm_map :
    AC_ASM_MAP id OBRACE isa_mapping_list CBRACE;

isa_mapping_list :
    isa_mapping_list isa_mapping |
    %empty;

isa_mapping :
    isa_mapping_group SEMICOLON |
    isa_mapping_strings SEMICOLON;

isa_mapping_group :
    isa_mapping_group_lrule EQUALS group;

isa_mapping_group_lrule :
    string group;

group :
    OBRACKET int GROUPING int CBRACKET;

isa_mapping_strings :
    isa_mapping_strings_lrule EQUALS int;

isa_mapping_strings_lrule :
    string_list_nonempty;

ar_predicated :
    AC_PREDICATED string;

isa_struct_definition :
    AC_STRUCT id OBRACE isa_struct_definition_entry_list CBRACE;

isa_struct_definition_entry_list :
    isa_struct_definition_entry_list isa_struct_definition_entry SEMICOLON |
    %empty;

isa_struct_definition_entry :
    id id;

/* String list */

string_list_nonempty :
    string_list_nonempty COMMA string |
    string;

/* ID List */

id_list :
    id_list_nonempty |
    %empty;

id_list_nonempty :
    id_list_nonempty COMMA id |
    id;

/* Expression Handling */

expression :
    expression operator expression |
    id |
    int;

operator :
    AC_PLUS |
    AC_MINUS;

expression_list :
    expression_list_nonempty |
    %empty;

expression_list_nonempty :
    expression_list_nonempty COMMA expression |
    expression;

%%

void ArchC::ArchCParser::error(const ArchC::location &location, const std::string &error)
{
    std::cerr << location << ":" << error << std::endl;
    //fprintf(stderr, "%s:%u: %s\n", location.begin.filename, location.begin.line, error.c_str());
}
